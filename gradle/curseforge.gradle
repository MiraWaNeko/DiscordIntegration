/*
 * Copyright (C) 2017 Chikachi
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see http://www.gnu.org/licenses.
 */

class CurseForgeDependencyInfo {
  public String module, version, ext
  private String fileid

  CurseForgeDependencyInfo(String module, String version, String ext, String fileid) {
    this.module = module
    this.version = version
    this.ext = ext
    this.fileid = fileid
  }

  String GetFileId() {
    return Integer.parseInt(fileid.substring(0, 4)) + "/" + Integer.parseInt(fileid.substring(4))
  }
}

def CurseForgeDependencyDownloader (dependencyInfo) {
  def downloadDir = "${rootProject.buildDir}/download/"
  File file = new File("${downloadDir}/${dependencyInfo.module}-${dependencyInfo.version}.${dependencyInfo.ext}")
  if (!file.exists()) {
    String url = "https://media.forgecdn.net/files/${dependencyInfo.GetFileId()}/${dependencyInfo.module}-${dependencyInfo.version}.${dependencyInfo.ext}"
    file.parentFile.mkdirs()
    printDebug "Download ${url}"
    new URL(url).withInputStream { downloadStream ->
      file.withOutputStream { fileOut ->
        fileOut << downloadStream
      }
    }
  }
  files(file.absolutePath)
}


ext {
  CurseForgeDependencyDownloader = this.&CurseForgeDependencyDownloader
}